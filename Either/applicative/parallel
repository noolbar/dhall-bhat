    let Applicative = ./../../Applicative/Type

in  let Semigroup = ./../../Semigroup/Type

in  let Either = ./../Type

in    λ(a : Type)
    → λ(semi : Semigroup a)
    →     ./../functor a
        ∧ { pure =
              λ(b : Type) → λ(x : b) → < Right : b | Left : a >.Right x
          , ap =
                λ(b : Type)
              → λ(c : Type)
              → λ(g : Either a (b → c))
              → λ(fa : Either a b)
              → merge
                { Left =
                      λ(l : a)
                    → merge
                      { Left =
                          λ(ll : a) → < Left : a | Right : c >.Left (semi.op l ll)
                      , Right =
                          λ(_ : b) → < Left : a | Right : c >.Left l
                      }
                      fa
                , Right =
                        let map = (./../functor a).map

                    in  λ(f : b → c) → map b c f fa
                }
                g
          }
      : Applicative (Either a)
